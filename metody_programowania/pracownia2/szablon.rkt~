#lang racket
(require data/heap)
(provide sim? wire?
         (contract-out
          [make-sim        (-> sim?)]
          [sim-wait!       (-> sim? positive? void?)]
          [sim-time        (-> sim? real?)]
          [sim-add-action! (-> sim? positive? (-> any/c) void?)]

          [make-wire       (-> sim? wire?)]
          [wire-on-change! (-> wire? (-> any/c) void?)]
          [wire-value      (-> wire? boolean?)]
          [wire-set!       (-> wire? boolean? void?)]

          [bus-value (-> (listof wire?) natural?)]
          [bus-set!  (-> (listof wire?) natural? void?)]

          [gate-not  (-> wire? wire? void?)]
          [gate-and  (-> wire? wire? wire? void?)]
          [gate-nand (-> wire? wire? wire? void?)]
          [gate-or   (-> wire? wire? wire? void?)]
          [gate-nor  (-> wire? wire? wire? void?)]
          [gate-xor  (-> wire? wire? wire? void?)]

          [wire-not  (-> wire? wire?)]
          [wire-and  (-> wire? wire? wire?)]
          [wire-nand (-> wire? wire? wire?)]
          [wire-or   (-> wire? wire? wire?)]
          [wire-nor  (-> wire? wire? wire?)]
          [wire-xor  (-> wire? wire? wire?)]

          [flip-flop (-> wire? wire? wire? void?)]))

; symulacja
(struct sim ([time #:mutable] [queue #:mutable])) ;; wartosci mutowalne

; kreator symulacji
(define (make-sim)
  (sim 0 (make-heap (lambda (x y)(< (car x)(car y)))))) ;; czas 0 kolejka pusta

; uruchomienie symulacji
(define (sim-wait! sim time)
  (define end-time (+ (sim-time sim) time))
  (define (it)
    (cond [(= (heap-count (sim-queue sim)) 0) (void)] ;; brak akcji
          [(> (car (heap-min (sim-queue sim))) end-time) (void)] ; nastepna akcja po danym czasie
          [else
           (let [(next-event (heap-min (sim-queue sim)))]
             (begin
               (heap-remove-min! (sim-queue sim))   ; usuwamy akcję
               (set-sim-time! sim (car next-event)) ; przesuwamy czas
               ((cdr next-event))                   ; wywolanie akcji
               (it)))]))
  (it)
  (set-sim-time! sim end-time))

; dodanie akcji
(define (sim-add-action! sim time action)
  (heap-add! (sim-queue sim)
             (cons (+ (sim-time sim) time) action)))

; przewody
(struct wire (sim [value #:mutable] [actions #:mutable] ))

; konstruktor przewodu
(define (make-wire sim)
  (wire sim #f null))

; dodanie akcji i wykonanie
(define (wire-on-change! wire action)
  (set-wire-actions! wire (cons action (wire-actions wire)))
  (action))

#|
a) akcje powinny być wykonane chronologicznie,
b) w momencie wywołania akcji czas symulacji
   powinien być ustawiony na czas przypisany do akcji
c) wywołanie akcji może spowodować pojawienie
   się nowy 
|#
; ustawienie wartosci
(define (wire-set! wire value)
  (if (equal? (wire-value wire) value)
      (void) ; bez zmian
      (begin
        (set-wire-value! wire value)
        (call-wire-actions wire))))

; wywolanie akcji
(define (call-wire-actions wire)
  (define (it actions)
    (cond [(null? actions) (void)]
          [else
           (begin ((car actions)) (it (cdr actions)))]))
  (it (wire-actions wire)))


(define (bus-set! wires value)
  (match wires
    ['() (void)]
    [(cons w wires)
     (begin
       (wire-set! w (= (modulo value 2) 1))
       (bus-set! wires (quotient value 2)))]))

(define (bus-value ws)
  (foldr (lambda (w value) (+ (if (wire-value w) 1 0) (* 2 value)))
         0
         ws))

; opoznienie bramek logicznych
(define not-delay  1)
(define and-delay  1)
(define nand-delay 1)
(define or-delay   1)
(define nor-delay  1)
(define xor-delay  2)


;wartosc logiczna sygnalow
(define (logical-not s)
  (cond
    [(eq? #f s) #t]
    [(eq? #t s) #f]
    [else (error "Niepoprawny sygnał: " s)]))

(define (logical-and s1 s2)
  (cond
    [(and (eq? #t s1) (eq? #t s2)) #t]
    [else #f]))

(define (logical-nand s1 s2)
  (logical-not (logical-and s1 s2)))

(define (logical-or s1 s2)
  (cond
    [(or (eq? #t s1) (eq? #t s2)) #t]
    [else #f]))

(define (logical-nor s1 s2)
  (logical-not (logical-or s1 s2)))

(define (logical-xor s1 s2)
  (cond
    [(eq? #t (logical-or s1 s2)) (logical-nand s1 s2)]
    [else #f]))

;--------------------------

(define (after-delay delay sim action)
  (sim-add-action! sim delay action))

;-----------------------------

(define (gate-not a out)
  (define (not-action-procedure)
    (let ((new-value
           (logical-not (wire-value a))))
      (after-delay not-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a not-action-procedure))


(define (gate-and a1 a2 out)
  (define (and-action-procedure)
    (let ((new-value
           (logical-and (wire-value a1)(wire-value a2))))
      (after-delay and-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a1 and-action-procedure)
  (wire-on-change! a2 and-action-procedure))

(define (gate-nand a1 a2 out)
  (define (nand-action-procedure)
    (let ((new-value
           (logical-nand ( wire-value a1)(wire-value a2))))
      (after-delay nand-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a1 nand-action-procedure)
  (wire-on-change! a2 nand-action-procedure))

(define (gate-or a1 a2 out)
  (define (or-action-procedure)
    (let ((new-value
           (logical-or ( wire-value a1)(wire-value a2))))
      (after-delay or-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a1 or-action-procedure)
  (wire-on-change! a2 or-action-procedure))

(define (gate-nor a1 a2 out)
  (define (nor-action-procedure)
    (let ((new-value
           (logical-nor ( wire-value a1)(wire-value a2))))
      (after-delay nor-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a1 nor-action-procedure)
  (wire-on-change! a2 nor-action-procedure))

(define (gate-xor a1 a2 out)
  (define (xor-action-procedure)
    (let ((new-value
           (logical-xor ( wire-value a1)(wire-value a2))))
      (after-delay xor-delay
                   (wire-sim out)
                   (lambda ()
                     (wire-set! out new-value)))))
  (wire-on-change! a1 xor-action-procedure)
  (wire-on-change! a2 xor-action-procedure))

;-------------------------------------

;;;;
(define (wire-not a1)
  (define out (make-wire (wire-sim a1)))
  (gate-not a1 out)
  out)

(define (wire-and a1 a2)
  (define out (make-wire (wire-sim a1)))
  (gate-and a1 a2 out)
  out)

(define (wire-nand a1 a2)
  (define out (make-wire (wire-sim a1)))
  (gate-nand a1 a2 out)
  out)

(define (wire-or a1 a2)
  (define out (make-wire (wire-sim a1)))
  (gate-or a1 a2 out)
  out)

(define (wire-nor a1 a2)
  (define out (make-wire (wire-sim a1)))
  (gate-nor a1 a2 out)
  out)

(define (wire-xor a1 a2)
  (define out (make-wire (wire-sim a1)))
  (gate-xor a1 a2 out)
  out)
;;;;
;----------------------------


(define (flip-flop out clk data)
  (define sim (wire-sim data))
  (define w1  (make-wire sim))
  (define w2  (make-wire sim))
  (define w3  (wire-nand (wire-and w1 clk) w2))
  (gate-nand w1 clk (wire-nand w2 w1))
  (gate-nand w2 w3 data)
  (gate-nand out w1 (wire-nand out w3)))

;---------------------------